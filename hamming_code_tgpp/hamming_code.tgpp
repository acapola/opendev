``
#find out the directory of this script
#that allow us to find the input files no matter what is the current directory
set scriptDir [file dirname [info script]]

#################
# utility procs #
#################
source [file join $scriptDir misc_utils.tcl]
proc minHammingDistance { vectors } {
	set len [llength $vectors]
	if {$len<2} {
		error "Need at least two element to compute hamming distance, got $len"
	}
	set out [expr 1<<32]
	for {set i 0} {$i<[llength $vectors]} {incr i} {
		set vectorI [lindex $vectors $i]
		#puts "$i --> $vectorI"
		for {set j [expr $i+1]} {$j<[llength $vectors]} {incr j} {
			set ijhd [hammingDistance $vectorI [lindex $vectors $j]]
			if {$ijhd<$out} {
				if {0==$ijhd} {return 0}
				set out $ijhd
			}
		}
	}
	return $out
}
proc nextWithHammingWeightSmallNumbersOnly { startVal targetHammingWeight {targetWidth 0} } {
    set out [expr $startVal+1]
    while {[hammingWeight $out]!=$targetHammingWeight} {
        incr out
        if {$targetWidth} {
            if {[binWidth $out]>$targetWidth} {return 0}
        }
    }
    return $out
}

proc firstWithHammingWeight {targetHammingWeight {targetWidth 0}} {
    set out 0
    for {set i 0} {$i<$targetHammingWeight} {incr i} {
        set out [expr $out + (1<<$i)]
    }
    if {$targetWidth} {assertEqual [expr [binWidth $out]>$targetWidth] 0}
    return $out
}
proc nextWithHammingWeight { startVal targetHammingWeight {targetWidth 0} } {
    if {$startVal} {
        #support only startVal with the target hamming weight
        assertEqual [hammingWeight $startVal] $targetHammingWeight
        if {$targetWidth} {assertEqual [expr [binWidth $startVal]>$targetWidth] 0}
    } else {
        #start the sequence
        return [firstWithHammingWeight $targetHammingWeight $targetWidth]
    }
    set ones [valToOnes $startVal]
    for {set bitIndex 0} {$bitIndex<$targetHammingWeight} {incr bitIndex} {
        #bitIndex refers to the list $ones
        #bitPos refers to the binary representation
       	set bitPos [lindex $ones $bitIndex]
        set nextBitPos [expr [lindex $ones $bitIndex] + 1]
        set nextBitIndex [expr $bitIndex + 1]
        if {(($nextBitIndex < $targetHammingWeight) && ($nextBitPos < [lindex $ones $nextBitIndex])) || (($nextBitIndex == $targetHammingWeight) && ($nextBitPos < $targetWidth)) } {
            #move the bit by one position left
            lset ones $bitIndex $nextBitPos
            if {$bitIndex>0} {
	            #re initialize all bits of lower significance
	            set lsbs [valToOnes [firstWithHammingWeight $bitIndex $bitPos]]
	            set ones [lreplace $ones 0 [expr $bitIndex-1] {*}$lsbs]
	          }
            return [onesToVal $ones]
        }
        #bit cannot be moved further, try next one
    }
    #all numbers enumerated
    return 0
}

##############
# Main procs #
##############

#compute various parameters used by other procs from the user friendly parameters
proc hammingCodeFunctionParams { inputWidth {extended 0} {userOutputWidth 0} } {
    # Computation of EDC parameters #
    #theory: 
    #message length k: number of data bits
    #code length r: number of code bits
    #block length n: total max number of bits (max data + code)
    #   k = 2^r - r - 1
    #   n = 2^r - 1
    
    set extendedArg [string tolower $extended]
    set ecc 0
    switch $extendedArg {
        0 { set extended 0 }
        1 -
        "extended" { set extended 1 }
        "ecc" {
            set extended 1
            set ecc 1
        }
        default {
            puts "unsupported value for extended parameter: $extendedArg"
            exit -1
        }
    }
    
    #we need to get r from k, we start from a lower bound and increment until we get a match 
    set k $inputWidth
    set r [binWidth [expr $k+1+2]]
    while {(1<<$r) - $r - 1 < $k} {
        incr r
    }
    if {$extended} {
    	#make sure extended equals either 0 or 1
    	set extended 1
    	incr r
    }
    if {$userOutputWidth} {
        if {$r>$userOutputWidth} {
            #the user request a width inferior to the lower bound, reject it (we can't garantee the advertized detection capabilities in this condition)
            puts "ERROR: requested output width is inferior to the lower bound."
            puts "At least $r bits are needed, but $userOutputWidth has been specified in the third arguement"
            exit -1
        }
        set r $userOutputWidth
    }
    dbgPutsVar r

    set n [expr (1<<$r) - 1]
    dbgPutsVar n

    set totalWidth [expr $r+$k]
    set nTotalWords [expr 1<<$totalWidth]
    set nValidWords [expr 1<<$k]
	
	set funcName hamming_code_${totalWidth}_$k

    if {$extended} {
        set funcName extended_$funcName
    }
    set eccFuncName "${funcName}_correction_pattern"
    return [dict create r $r k $k n $n totalWidth $totalWidth nTotalWords $nTotalWords nValidWords $nValidWords funcName $funcName eccFuncName $eccFuncName]
}

proc hammingCodeFunctionName { inputWidth {extended 0} {userOutputWidth 0} } {
    set parameters [hammingCodeFunctionParams $inputWidth $extended $userOutputWidth]
    return [dict get $parameters funcName]
}

#compute the EDC code
proc hammingCode { inputWidth {extended 0} {userOutputWidth 0} } {
    set parameters [hammingCodeFunctionParams $inputWidth $extended $userOutputWidth]
    dictToVars $parameters

    # main algorithm #
    set targetHammingWeight [expr 2+$extended]
    #build a list of numbers with hamming weight targetHammingWeight and width r
    set vcodes [list]
    set code 0
    while {[set code [nextWithHammingWeight $code $targetHammingWeight $r]]} {lappend vcodes $code}

	#bits: map syndrom bit number to the list of input bits to compute it
    set bits [dict create]
	#bitsInputs: map syndrom bit number to the number of inputs it has
    set bitsInputs [dict create]
    for {set i 0} {$i<$r} {incr i} {
        dict set bits $i [list]
        dict set bitsInputs $i 0
    }
    for {set col 0} {$col<$k} {incr col} {
        if {[llength $vcodes]==0} {
            #we ran out of codes, get the next batch
            incr targetHammingWeight
           	if {$extended} {
           		#keep the hamming weight odd, we cannot get a min distance of 3 if we use even hamming weights here.
           		incr targetHammingWeight
           	}
            set code 0
            while {[set code [nextWithHammingWeight $code $targetHammingWeight $r]]} {lappend vcodes $code}
        }
        #find the bits with the least inputs
        dbgPuts "$bitsInputs -> [sortDictByValue $bitsInputs -integer]" 1
        set bitsInputs [sortDictByValue $bitsInputs -integer]
        set rank 0
        set candidateCodes $vcodes
        for {set i 0} {$i<$targetHammingWeight} {incr i} {
            dbgPutsVar i 2
            while 1 {
                set minInputRow [lindex [dict keys $bitsInputs] $rank]
                dbgPutsVar minInputRow 3
                set newCandidateCodes [filter $candidateCodes {expr (1<<$minInputRow) & }]
                incr rank
                if {[llength $newCandidateCodes]} {
                    #found a match, add the bit to the row
                    dict lappend bits $minInputRow $col
                    dict incr bitsInputs $minInputRow
                    break
                }
            }
            set candidateCodes $newCandidateCodes
        }
        set code [lindex $candidateCodes 0]    
        lremove vcodes $code
        dbgPuts "[format %2d $col]: [bitsSet $code] ([format %2d $code]) [dict keys $bitsInputs] - [dict values [dict_sort $bitsInputs]]" 1
        dbgPuts ""
    }
    #set totalWidth [expr $r+$k]
    #set nTotalWords [expr 1<<$totalWidth]
	
	set codeDescription "a hamming code capable of detecting any 1 and 2 bits errors"

    if {$extended} {
        set funcName extended_$funcName
        set codeDescription "an extended hamming code capable of detecting any 1,2 and 3 bits errors"
    }

    set indexesWidth [decWidth $k]
    set syndromIndexesWidth [decWidth $r]
    assertEqual $r [llength [dict keys $bits]]
	
	set out [dict create]
	dict set out inputWidth $inputWidth
	dict set out extended $extended
	dict set out ecc 0
	dict set out userOutputWidth $userOutputWidth
	
	dict set out parameters $parameters
	
	dict set out funcName [dict get $parameters funcName]
	dict set out eccFuncName [dict get $parameters eccFuncName]
	dict set out r [dict get $parameters r]
	dict set out k [dict get $parameters k]
	dict set out nValidWords [dict get $parameters nValidWords]
	dict set out nTotalWords [dict get $parameters nTotalWords]
	
	dict set out codeDescription $codeDescription
	dict set out syndromIndexesWidth $syndromIndexesWidth
	dict set out indexesWidth $indexesWidth
	dict set out bitsInputs $bitsInputs
	dict set out bits $bits

	hammingCodeBalanceLogicDepth out	
	hammingCodeDotsAndLoads out
	
	return $out
}

proc hammingCodeBalanceLogicDepth { hammingCodeDictName } {
	upvar $hammingCodeDictName hammingCodeDict 
	dictToVars $hammingCodeDict
	
	set syndroms [dictTransposeIndexes $bits]
	assertEqual 0 [dictHasDuplicates $syndroms]
	#printIntDict $syndroms
	
	set stats [dictMinMax $bitsInputs]
	set minLogicDepth [dict get $stats minValue]
	set maxLogicDepth [dict get $stats maxValue]
	if {$minLogicDepth+1<$maxLogicDepth} { #if unbalanced mapping
		set minLogicDepthBits [dict get $stats minIndexes]
		#mark the bits already occupied in the syndrom bits with min inputs
		set minOccupied [dict create] 
		foreach syndromBit $minLogicDepthBits {
			dict lappend minOccupied $syndromBit {*}[dict get $bits $syndromBit]
		}
		set maxLogicDepthBits [dict get $stats maxIndexes]
		#mark the bits in the syndrom bits with max input, any of them is a candidate for move.
		set maxCandidates [dict create] 
		foreach syndromBit $maxLogicDepthBits {
			dict lappend maxCandidates $syndromBit {*}[dict get $bits $syndromBit]
		}
		#enumerate the possible moves
		set possibleMoves [dict create]
		foreach longPath [dict key $maxCandidates] {
			set longPathBits [dict get $maxCandidates $longPath]
			foreach candidate $longPathBits {
				#what the long path would look like without this candidate:
				#set newLongPath [lsearch -not -all -exact -inline $longPathBits $candidate]
				#set newLongPath $longPathBits
				#lremove newLongPath $candidate
				foreach shortPath [dict key $minOccupied] {
					set shortPathBits [dict get $minOccupied $shortPath]
					if {![lcontains $shortPathBits $candidate]} { #if shortPath does not contain the candidate, take a closer look
						#set newShortPath [lsort [lappend $shortPath $candidate]]
						set newSyndroms $syndroms
						#set oldCandidateInputSyndrom [dict get $syndroms $candidate]
						#set newCandidateInputSyndrom [lsearch -not -all -exact -inline $oldCandidateInputSyndrom $longPath] 
						set newCandidateInputSyndrom [dict get $syndroms $candidate]
						lremove newCandidateInputSyndrom $candidate
						
						lappend newCandidateInputSyndrom {*}$shortPath
						set newCandidateInputSyndrom [lsort -integer $newCandidateInputSyndrom]
						dict set newSyndroms $candidate $newCandidateInputSyndrom
						#printIntDict $newSyndroms
						if {![dictHasDuplicates $newSyndroms]} {
							#puts "can move $candidate from $longPath to $shortPath"
							dict lappend possibleMoves $longPath [dict create $shortPath $candidate]
						}
					}
				}
			}
		}
		#move the bits on a first arrived first served basis (clearly not perfect, but good enough for current use cases)
		set shortPathsIncreased [list]
		dict for {longPath movs} $possibleMoves {
			set nMovs [llength $movs]
			set done 0
			set i 0
			while {($i<$nMovs) && !$done} {
				set mov [lindex $movs 0]
				set shortPath [dict keys $mov]
				set candidate [dict values $mov]
				if {![lcontains $shortPathsIncreased $shortPath]} { #if we did not already increase this short path
					#puts "moving $candidate from $longPath to $shortPath"
					lappend shortPathsIncreased shortPath
					set newBits $bits
					set syndromBits [dict get $bits $longPath]
					lremove syndromBits $candidate
					dict set newBits $longPath $syndromBits
					set syndromBits [dict get $bits $shortPath]
					set syndromBits [lsort -integer [lappend syndromBits $candidate]]
					dict set newBits $shortPath $syndromBits
					set bits $newBits
					dict incr bitsInputs $longPath -1
					dict incr bitsInputs $shortPath 
					set done 1
				} else {
					incr i
				}
			}
		}
	}
	set stats [dictMinMax $bitsInputs]
	set minLogicDepth [dict get $stats minValue]
	set maxLogicDepth [dict get $stats maxValue]
	if {$minLogicDepth+1<$maxLogicDepth} { #if unbalanced mapping
		puts "WARNING: could not balance logic depth on all syndrom bits in function $funcName"
	}
	dict set hammingCodeDict bits $bits
	dict set hammingCodeDict bitsInputs $bitsInputs
}

proc hammingCodeDotsAndLoads { hammingCodeDictName } {
	upvar $hammingCodeDictName hammingCodeDict 
	dictToVars $hammingCodeDict
	
	set loads [dict create]
    set dots [dict create]
    # results formatting to generate output #
    for {set syndromBit 0} {$syndromBit<$r} {incr syndromBit} {
        #loads is used later on in the input usage report.
        foreach b [dict get $bits $syndromBit] {
            dict lappend loads $b $syndromBit
        }
        set inBits  [dict get $bits $syndromBit]
        for {set b 0} {$b<$k} {incr b} {
             if {[lsearch $inBits $b]!=-1} { 
                dict append dots $syndromBit "x" 
             } else { 
                dict append dots $syndromBit " " 
             }
        }
    }
	dict set hammingCodeDict dots $dots
	dict set hammingCodeDict loads $loads
}

proc hammingCodeCorrectionPattern { hammingCodeDictName } {
	upvar $hammingCodeDictName hammingCodeDict 
	dictToVars $hammingCodeDict
	
	set tclModelProc [hammingCodeTclProcNumbers $hammingCodeDict]
	eval $tclModelProc
	
	set eccPatterns [dict create]
	for {set i 0} {$i<$k} {incr i} {
		set syndromBits [$funcName [expr 1<<$i]]
        dict set eccPatterns $i [string map {" " ""} [valToBits $syndromBits $r]]
    }
	
	dict set hammingCodeDict eccUncorrectableError 0
	dict set hammingCodeDict eccCorrectableError 0
	dict set hammingCodeDict eccPatterns $eccPatterns
	dict set hammingCodeDict ecc 1
}

proc hammingCodeCommentsC { hammingCodeDict } {
	dictToVars $hammingCodeDict
``
//`$funcName`
//Compute `$r` bits Error Detection Code from a `$k` bits input.
//The EDC is `$codeDescription` in the input data or the EDC.
//There are `$nValidWords` valid code words out of `$nTotalWords` therefore `expr round(($nTotalWords-$nValidWords)*100/$nTotalWords)`% of errors are detected. 
//Dot graphic view: in[0]...in[`expr $k-1`]
``for {set syndromBit 0} {$syndromBit<$r} {incr syndromBit} {``
//  syndrom[`format %${syndromIndexesWidth}s $syndromBit`]: `dict get $dots $syndromBit` (`dict get $bitsInputs $syndromBit` inputs)
``}``
//Input usage report:
``for {set inBit 0} {$inBit<$k} {incr inBit} {
    set inBitLoads [dict get $loads $inBit]
``
//  input bit `format %${indexesWidth}s $inBit` used `format %${syndromIndexesWidth}s [llength $inBitLoads]` times (syndrom bits `$inBitLoads`)
``}
    #return all except the last new line character
    return [string range [::tgpp::getProcOutput] 0 end-1]
} 
#end of hammingCodeFunction

#check by verifying that no syndrom is identical
#if the code is an extended code, call the dedicated proc checkExtendedHammingCode
proc checkHammingCode { hammingCodeDict } {
	dictToVars $hammingCodeDict
	if { $extended } { 
		checkExtendedHammingCode $hammingCodeDict 
		return
	}
	set actualOutputWidth $r
	set tclModelProc [hammingCodeTclProcNumbers $hammingCodeDict]
	eval $tclModelProc
	set syndroms [list]
	for {set inputBit 0} {$inputBit<$inputWidth} {incr inputBit} {
		lappend syndroms [$funcName (1<<$inputBit)]
	} 
	if {[lHasDuplicates $syndroms]} {
		error "INTERNAL ERROR: duplicated syndroms found:\n${syndroms}\nPlease report this error."
	}
}

#extended case: check by verifying that the sum of every triplet of syndrom is not 0
proc checkExtendedHammingCode { hammingCodeDict } {
	dictToVars $hammingCodeDict
	set actualOutputWidth $r
	set tclModelProc [hammingCodeTclProcNumbers $hammingCodeDict]
	eval $tclModelProc
	set syndroms [list]
	for {set inputBit 0} {$inputBit<$inputWidth} {incr inputBit} {
		lappend syndroms [$funcName (1<<$inputBit)]
	}	
	#basic test to check that we have at least a normal hamming code
	if {[lHasDuplicates $syndroms]} {
		error "INTERNAL ERROR: duplicated syndroms found:\n${syndroms}\nPlease report this error."
	}
	#test triplets
	set syndromsLen [llength $syndroms]
	for {set i 0} {$i<$syndromsLen} {incr i} {
		set syndromI [lindex $syndroms $i]
		for {set j [expr $i+1]} {$j<$syndromsLen} {incr j} {
			set syndromIJ [expr $syndromI ^ [lindex $syndroms $j]]
			for {set k [expr $j+1]} {$k<$syndromsLen} {incr k} {
				set syndromIJK [expr $syndromIJ ^ [lindex $syndroms $k]]
				if { !$syndromIJK } {
					set msg "INTERNAL ERROR: a triplet of syndroms is null: $i $j $k\n"
					append msg "[valToBits $syndromI $r] ([lindex $syndroms $i])\n"
					append msg "[valToBits [lindex $syndroms $j] $r] ([lindex $syndroms $j])\n"
					append msg "[valToBits [lindex $syndroms $k] $r] ([lindex $syndroms $k])\n"
					append msg "${syndroms}\n"
					append msg "Please report this error."
					error $msg
				}
			}
		}
	}
	
	if {0 & $ecc} {
		#test few faults for each bit position
		for {set i 0} {$i<$inputWidth} {incr i} {
			set fault [expr 1<<$i]
			set data [random $inputWidth]
			set expectedSyndrom [bitsToVal [$funcName $data]]
			set faultyData [expr $data ^ $fault]
			set syndrom [bitsToVal [$funcName $faultyData]]
			set deltaSyndrom [expr $expectedSyndrom ^ deltaSyndrom]
			set deltaSyndromBits [valToBits $deltaSyndrom $r]
			set analysis [$eccFuncName $deltaSyndromBits]
			if {[dict get $analysis correctable_error]} {
				
			}
		}	
		
	}
}

#check by computing the minimum hamming distance over all code words: does not scale well...
proc checkHammingCodeByMinDistance { hammingCodeDict } {
	dictToVars $hammingCodeDict
	set actualOutputWidth $r
	set tclModelProc [hammingCodeTclProcNumbers $hammingCodeDict]
	#puts $tclModelProc
	eval $tclModelProc
	
	set validWords [list]
	if {$inputWidth<16} {
		#exhaustive checking for small input width
		for {set i 0} {$i<$nValidWords} {incr i} {
			set syndromBits [$funcName $i]
			set word [expr $i | ($syndromBits<<$inputWidth)]
			lappend validWords $word
		}
		set minHd [minHammingDistance $validWords]
		puts "Achieved minimum hamming distance: $minHd"
	} else {
		set base [random $inputWidth]
		set mask [expr (1<<$inputWidth) - 1]
		set testSize 100
		puts "Estimating minimum hamming distance using $testSize consecutive values"
		puts "Base: $base"
		for {set i $base} {$i<$base+$testSize} {incr i} {
			set in [expr $i & $mask]
			set syndromBits [$funcName $in]
			set word [expr $in | ($syndromBits<<$inputWidth)]
			lappend validWords $word
			#puts [string map {" " ""} [valToBits $word [expr $inputWidth+$actualOutputWidth]]]
		}
		set minHd [minHammingDistance $validWords]
		puts "Estimated minimum hamming distance: $minHd"
	}
	set expectedMinHd [expr 3+$extended]
	if {$minHd < $expectedMinHd} {
		error "INTERNAL ERROR: expected minimum hamming distance is $expectedMinHd, generated code achieve $minHd. Please report this error."
	}
}

#verilog output
tgpp::source [file join $scriptDir hamming_code.tgpp.v]
tgpp::source [file join $scriptDir hamming_code.tgpp.tcl]
tgpp::source [file join $scriptDir hamming_code.tgpp.dat]

##################################
# command line arguments parsing #
##################################
#parse command line only if this file is executed as a stand alone generator (ie. not included in another file)
dbgPuts "[info ex argv0] && [file tail [info script]] == [file tail $argv0]"
if {[info ex argv0] && [file tail [info script]] == [file tail $argv0]} {

    if { ($argc < 1) || ($argc >6) } {
        puts "ERROR: $argc argument provided, need 1 or up to 6."
        puts "argc=$argc, argv=$argv"
        puts "arguments: input_width \[extended\] \[output_width\] \[output_type\]"
		puts "supported value for extended: 0, 1, extended, ecc
        puts "supported value for output_type: verilog, tcl, vectors
        exit -1
    }
    set inputWidth [lindex $argv 0]
    #default values for optional parameters
    set extended 0
	set ecc 0
    set userOutputWidth 0
    set verilogOutput 0
	set tclOutput 0
	set vectorsOutput 0
	if { [llength $argv] >= 2 } {
		set extendedArg [string tolower [lindex $argv 1]]
        switch $extendedArg {
			0 { set extended 0 }
			1 -
			"extended" { set extended 1 }
			"ecc" {
				set extended 1
				set ecc 1
			}
			default {
				puts "unsupported value for extended parameter: $extendedArg"
				exit -1
			}
        }
    }
    if { [llength $argv] >= 3 } {
        #user specified output width
        #withdraw the optional parity bit from that count to have a direct comparison with the r parameter computed later on.
        #set userOutputWidth [expr [lindex $argv 2] - $extended]
		set userOutputWidth [lindex $argv 2]
    }
    if { [llength $argv] <= 3 } {
		#no output_type specified, enable verilog output by default
		set verilogOutput 1
	} else {
		for {set i 3} {$i<[llength $argv]} {incr i} {
			set output [lindex $argv $i]
			#puts $output
			#if { $output == "verilog" } { set verilogOutput 1 } 
			#if { $output == "tcl" } { set tclOutput 1 } 
			#if { $output == "vectors" } { set vectorsOutput 1 }
			switch $output {
				verilog {set verilogOutput 1}
				tcl     {set tclOutput 1}
				vectors {set vectorsOutput 1}
				default {
					puts "ERROR: $output output type not supported."
					puts "Supported value for output_type: verilog, tcl, vectors
					exit -1
				}
			}
		}
	}
	
	dbgPutsVar inputWidth
	dbgPutsVar extended
	dbgPutsVar userOutputWidth
	
    #compute the code
	set hammingCodeDict [hammingCode $inputWidth $extended $userOutputWidth]
	if {$ecc} {
		#compute the corrector part
		hammingCodeCorrectionPattern hammingCodeDict
	}
	#do a bit of self checking
	checkHammingCode $hammingCodeDict
	
	#the actual output to file take place in the following lines
	if {$verilogOutput} {
	#::tgpp::renameOutput hamming_code.v
		::tgpp::off all
		::tgpp::on hamming_code.v
``
`hammingCodeVerilogFunction2 $hammingCodeDict $ecc`
``  
	}
	if {$tclOutput} {
		::tgpp::off all
		::tgpp::on hamming_code.tcl
``
`hammingCodeTclProcNumbers $hammingCodeDict`
``  
	}
	if {$vectorsOutput} {
		::tgpp::off all
		::tgpp::on hamming_code.dat
		set tclModelProc [hammingCodeTclProc2 $hammingCodeDict]
		#puts $tclModelProc
		eval $tclModelProc
``
`hammingCodeVectors2 $hammingCodeDict`
``
	}
	#delete default output
	::tgpp::renameOutput tmp
	::tgpp::renameOutput tmp ""
}
``
